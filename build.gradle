apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
    mavenCentral()
}

group = 'com.tkruse.gradle'
version = '0.0.1-SNAPSHOT'

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Groovysh plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar( type: Jar ) {
    classifier 'javadoc'
    from groovydoc
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

task wrapper(type: Wrapper, description: 'Generates the gradle wrapper.') {
    gradleVersion = '1.12'
}

test {
    testLogging {
        exceptionFormat "full" // default is "short"
    }
}

dependencies {
    compile gradleApi()
    compile "jline:jline:1.0"
    compile('org.codehaus.groovy:groovy-all:2.2.2') { force = true }
    runtime('commons-cli:commons-cli:1.2')
//    compile("jline:jline:2.11") {
//        exclude(group: 'junit', module: 'junit')
//    }
    testCompile("junit:junit-dep:4.11")
}
